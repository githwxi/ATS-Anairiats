(***********************************************************************)
(*                                                                     *)
(*                         Applied Type System                         *)
(*                                                                     *)
(*                              Hongwei Xi                             *)
(*                                                                     *)
(***********************************************************************)

(*
** ATS - Unleashing the Power of Types!
**
** Copyright (C) 2002-2007 Hongwei Xi, Boston University
**
** All rights reserved
**
** ATS is  free software;  you can redistribute it and/or modify it under
** the  terms of the  GNU General Public License as published by the Free
** Software Foundation; either version 2.1, or (at your option) any later
** version.
** 
** ATS is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without  even  the  implied  warranty  of MERCHANTABILITY or
** FITNESS FOR A PARTICULAR PURPOSE.  See the  GNU General Public License
** for more details.
** 
** You  should  have  received  a  copy of the GNU General Public License
** along  with  ATS;  see  the  file  COPYING.  If not, write to the Free
** Software Foundation, 51  Franklin  Street,  Fifth  Floor,  Boston,  MA
** 02110-1301, USA.
*)

(* ****** ****** *)

//
// Author: Artyom Shalkhakov (artyom DOT shalkhakov AT gmail DOT com)
// Time: December, 2011
//

(* ****** ****** *)

(*
**
** A simple calculator for evaluating arithmetic expressions.
** With modifications to [calc.lats], this example
** shows how to write re-entrant lexers in ATS.
**
*)

(*

The formats of supported commands:

[var] = [exp]
print [exp]
quit

Here is a session:

./calc
<< x = 1 + 2
<< y = x + x
<< print x
>> 3
<< print y
>> 6
<< quit

*)

%{

// preamble

staload "libc/SATS/stdio.sats"
staload "libats/lex/lexing.sats"

(* ****** ****** *)

exception UncloseCommentException

fun comment_is_unclosed (lb: &lexbuf_t): void = begin
  exit_errmsg {void} (1, "unclosed comment\n") ;
end // end of [comment_is_unclosed]

dataviewtype token =
  | TOKid of strptr1
  | TOKint of int
  | TOKop_add | TOKop_sub | TOKop_mul | TOKop_div
  | TOKparen_l | TOKparen_r | TOKeq | TOKeof
// end of [token]

fun free_token (tok: token): void = case+ tok of
  | ~TOKid id => strptr_free id
  | ~TOKint i => ()
  | ~TOKop_add () => ()
  | ~TOKop_sub () => ()
  | ~TOKop_mul () => ()
  | ~TOKop_div () => ()
  | ~TOKparen_l () => ()
  | ~TOKparen_r () => ()
  | ~TOKeq () => ()
  | ~TOKeof () => ()
// end of [free_token]

extern fun fprint_token {m:file_mode}
  (pf_mod: file_mode_lte (m, w) | fil: &FILE m, tok: !token): void

implement fprint_token
  (pf_mod | fil, tok): void = case+ tok of
  | TOKid !s =>
      (fprint (pf_mod | fil, "TOKid(");
       fprint (pf_mod | fil, !s);
       fprint (pf_mod | fil, ")");
       fold@ tok)
  | TOKint i =>
      (fprintf (pf_mod | fil, "TOKint(%i)", @(i)); fold@ tok)
  | TOKop_add () =>
      (fprint_string (pf_mod | fil, "+"); fold@ tok)
  | TOKop_sub () =>
      (fprint_string (pf_mod | fil, "-"); fold@ tok)
  | TOKop_mul () =>
      (fprint_string (pf_mod | fil, "*"); fold@ tok)
  | TOKop_div () =>
      (fprint_string (pf_mod | fil, "/"); fold@ tok)
  | TOKparen_l () =>
      (fprint_string (pf_mod | fil, "("); fold@ tok)
  | TOKparen_r () =>
      (fprint_string (pf_mod | fil, ")"); fold@ tok)
  | TOKeq () =>
      (fprint_string (pf_mod | fil, "="); fold@ tok)
  | TOKeof () =>
      (fprint_string (pf_mod | fil, "EOF"); fold@ tok)
// end of [fprint_token]

extern fun print_token (tok: !token): void

implement print_token (tok) = let
  val (pf_stdout | stdout) = stdout_get ()
in
  fprint_token (file_mode_lte_w_w | !stdout, tok);
  stdout_view_set (pf_stdout | (*none*))
end // end of [print_token]

extern fun prerr_token (tok: !token): void

implement prerr_token (tok) = let
  val (pf_stderr | stderr) = stderr_get ()
in
  fprint_token (file_mode_lte_w_w | !stderr, tok);
  stderr_view_set (pf_stderr | (*none*))
end // end of [prerr_token]

(* ****** ****** *)

%{^

extern long int
strtol (const char *nptr, char **endptr, int base) ;

ats_int_type __strtoi
  (ats_ptr_type str, ats_int_type base) {
  return strtol ((char*)str, (char**)0, base) ;
} // end of [__strtoi]

%}

fun lexeme_int (lb: &lexbuf_t, base: int): int = let
  val str = lexeme_strptr_lexbuf (lb)
  val res = __strtoi (str, base) in strptr_free str; res
end where {
  extern fun __strtoi (str: !strptr1, base: int): int = "__strtoi"
} // end of [lexeme_int]

(* ****** ****** *)

extern fun TOKEN (mylexbuf: &lexbuf_t): token
fn TOKEN_lexing_error {a:viewt@ype} (mylexbuf: &lexbuf_t): a = lexing_error ()

extern fun COMMENT (mylexbuf: &lexbuf_t, level: int): void
fn COMMENT_lexing_error {a:viewt@ype} (mylexbuf: &lexbuf_t, level: int): a = lexing_error ()

extern fun COMMENT_LINE (mylexbuf: &lexbuf_t): void
fn COMMENT_LINE_lexing_error {a:viewt@ype} (mylexbuf: &lexbuf_t): a = lexing_error ()

extern fun COMMENT_REST (mylexbuf: &lexbuf_t): void
fn COMMENT_REST_lexing_error {a:viewt@ype} (mylexbuf: &lexbuf_t): a = lexing_error ()

(* ****** ****** *)

%}

// definition of constants of regular expressions

(* 011: \t; \014: \f; \015: \r *)
blank = [ '\f' '\r' '\t' ' ' ] +

xdigit = [ '0'-'9' 'a'-'f' 'A'-'F' ]

ident_fst = [ 'A'-'Z' 'a'-'z' '_' ]
ident_rst = [ '0'-'9' 'A'-'Z' 'a'-'z' '_' '\'' ]

ident = $ident_fst $ident_rst *

sgn = ['+' '-']?
int8 = $sgn '0' ['0'-'7']*
int10 = $sgn ['1'-'9'] ['0'-'9']*
int16 = $sgn '0' [ 'x' 'X'] $xdigit+

newline = '\n'

%%

TOKEN () =
  | $blank { TOKEN (mylexbuf) }
  | $newline { TOKEN (mylexbuf) }
  | "(*" { COMMENT (mylexbuf, 0); TOKEN (mylexbuf) }
  | "//" { COMMENT_LINE (mylexbuf); TOKEN (mylexbuf) }
  | "////" { COMMENT_REST (mylexbuf); TOKeof () }
  | $ident { TOKid (lexeme_strptr_lexbuf (mylexbuf)) }
  | $int8 { TOKint (lexeme_int (mylexbuf, 8)) }
  | $int10 { TOKint (lexeme_int (mylexbuf, 10)) }
  | $int16 { TOKint (lexeme_int (mylexbuf, 16)) }
  | '=' { TOKeq () }
  | '+' { TOKop_add () }
  | '-' { TOKop_sub () }
  | '*' { TOKop_mul () }
  | '/' { TOKop_div () }
  | '(' { TOKparen_l () }
  | ')' { TOKparen_r () }
  | $EOF { TOKeof () }

COMMENT (level) =
  | "(*" { COMMENT (mylexbuf, level+1) }
  | "*)" { if level > 0 then COMMENT (mylexbuf, level-1) else () }
  | $newline { COMMENT (mylexbuf, level) }
  | $EOF { comment_is_unclosed (mylexbuf) }
  | [^] { COMMENT (mylexbuf, level) }

COMMENT_LINE () =
  | [^ '\n']* { () }

COMMENT_REST () =
  | [^]* { () }

%%

%{^
#include "libc/CATS/stdio.cats"
%} // end of [%{^]

// postamble in the lexer.sats

dataviewtype exp =
  | EXPint of int
  | EXPadd of (exp, exp)
  | EXPsub of (exp, exp)
  | EXPmul of (exp, exp)
  | EXPdiv of (exp, exp)
  | EXPid of strptr1
  | EXPerr

fun free_exp (e0: exp): void = case+ e0 of
  | ~EXPint i => ()
  | ~EXPadd (e1, e2) => (free_exp e1; free_exp e2)
  | ~EXPsub (e1, e2) => (free_exp e1; free_exp e2)
  | ~EXPmul (e1, e2) => (free_exp e1; free_exp e2)
  | ~EXPdiv (e1, e2) => (free_exp e1; free_exp e2)
  | ~EXPid id => strptr_free id
  | ~EXPerr () => ()
// end of [free_exp]

fun print_exp (e0: !exp): void = case+ e0 of
  | EXPint i => begin
      print "EXPint("; print i; print ")"; fold@ e0
    end
  | EXPadd (!p_e1, !p_e2) => begin
      print "EXPadd("; print_exp !p_e1; print ", "; print_exp !p_e2; print ")"; fold@ e0
    end
  | EXPsub (!p_e1, !p_e2) => begin
      print "EXPsub("; print_exp !p_e1; print ", "; print_exp !p_e2; print ")"; fold@ e0
    end
  | EXPmul (!p_e1, !p_e2) => begin
      print "EXPmul("; print_exp !p_e1; print ", "; print_exp !p_e2; print ")"; fold@ e0
    end
  | EXPdiv (!p_e1, !p_e2) => begin
      print "EXPdiv("; print_exp !p_e1; print ", "; print_exp !p_e2; print ")"; fold@ e0
    end
  | EXPid (!id) => (print "EXPid("; print (!id); print ")"; fold@ e0)
  | EXPerr () => (print "EXPerr("; print ")"; fold@ e0)
// end of [print_exp]

dataviewtype cmd =
  | CMDassgn of (strptr1, exp)
  | CMDerror
  | CMDprint of exp
  | CMDquit

fun free_cmd
  (cmd: cmd): void = begin case+ cmd of
  | ~CMDassgn (id, e) => (strptr_free id; free_exp e)
  | ~CMDerror () => ()
  | ~CMDprint e => free_exp e
  | ~CMDquit () => ()
end // end of [free_cmd]

//

exception EvalErrorException

(*

[var] := [identifier]
[exp0] :=  [var] | [integer] | ( [exp2] )
[exp0_r] := * [exp0] | / [exp0] | (* empty *)
[exp1] := [exp0] [exp1_r]
[exp1_r] := + [exp1] | - [exp1] | (* empty *)
[exp2] := [exp1] [exp1_r]

*)

fun parse_paren_r
  (lb: &lexbuf_t, tok: &token, err: &int): void =
  case+ tok of
  | ~TOKparen_r () => (tok := TOKEN (lb))
  | tok_v => (tok := tok_v; err := err + 1)
// end of [parse_paren_r]

fun parse_exp_0
  (lb: &lexbuf_t, tok: &token, err: &int): exp =
  case tok of
  | ~TOKint i => (tok := TOKEN (lb); EXPint i)
  | ~TOKid id => (tok := TOKEN (lb); EXPid id)
  | ~TOKparen_l () => let
      val () = tok := TOKEN (lb)
      val e = parse_exp_2 (lb, tok, err)
    in
      parse_paren_r (lb, tok, err); e
    end
  | tok_v => begin
      free_token tok_v; err := err + 1; tok := TOKEN (lb); EXPerr
    end
// end of [parse_exp_0]

and parse_exp_0_r
  (lb: &lexbuf_t, tok: &token, err: &int, e0: exp): exp =
  case tok of
  | ~TOKop_mul () => let
      val () = tok := TOKEN (lb)
      val e1 = parse_exp_0 (lb, tok, err)
    in
      parse_exp_0_r (lb, tok, err, EXPmul (e0, e1))
    end
  | ~TOKop_div () => let
      val () = tok := TOKEN (lb)
      val e1 = parse_exp_0 (lb, tok, err)
    in
      parse_exp_0_r (lb, tok, err, EXPdiv (e0, e1))
    end
  | tok_v => (tok := tok_v; e0)
// end of [parse_exp_0_r]

and parse_exp_1
  (lb: &lexbuf_t, tok: &token, err: &int): exp = let
  val e0 = parse_exp_0 (lb, tok, err) in parse_exp_0_r (lb, tok, err, e0)
end // end of [parse_exp_1]

and parse_exp_1_r
  (lb: &lexbuf_t, tok: &token, err: &int, e0: exp): exp =
  case tok of
  | ~TOKop_add () => let
      val () = tok := TOKEN (lb)
      val e1 = parse_exp_1 (lb, tok, err)
    in
      parse_exp_1_r (lb, tok, err, EXPadd (e0, e1))
    end
  | ~TOKop_sub () => let
      val () = tok := TOKEN (lb)
      val e1 = parse_exp_1 (lb, tok, err)
    in
      parse_exp_1_r (lb, tok, err, EXPsub (e0, e1))
    end
  | tok_v => (tok := tok_v; e0)
// end of [parse_exp_1_r]

and parse_exp_2 (lb: &lexbuf_t, tok: &token, err: &int): exp =
  let val e0 = parse_exp_1 (lb, tok, err) in parse_exp_1_r (lb, tok, err, e0) end
// end of [parse_exp_2]

//

fun parse_eq
  (lb: &lexbuf_t, tok: &token, err: &int): void =
  case+ tok of
  | ~TOKeq () => (tok := TOKEN (lb))
  | tok_v => (tok := tok_v; err := err + 1)
// end of [parse_eq]

//

(*

[cmd] := [var] = [exp2] | print [exp2] | quit

*)

fun parse_cmd
  (lb: &lexbuf_t, tok: &token, err: &int): cmd = let
  extern
  fun eq_strptr_string (x: !strptr1, y: string)
    : bool = "atspre_eq_string_string"
  // end of [eq_strptr_string]
  overload = with eq_strptr_string
in
  case+ tok of
    | ~TOKid id when id = "print" => let
        val () = strptr_free id
        val () = tok := TOKEN (lb)
        val e = parse_exp_2 (lb, tok, err)
      in
        CMDprint e
      end
    | ~TOKid id when id = "quit" =>
      (strptr_free id; tok := TOKEN (lb); CMDquit ())
    | ~TOKid id => let
        val () = tok := TOKEN (lb)
        val () = parse_eq (lb, tok, err)
        val e = parse_exp_2 (lb, tok, err)
      in
        CMDassgn (id, e)
      end
    | tok_v => (tok := tok_v; CMDerror ())
end // end of [parse_cmd]

//

dataviewtype env = ENVnil | ENVcons of (strptr1, int, env)

fun free_env (env: env): void = case+ env of
  | ~ENVcons (id, _, env1) => (free_env env1; strptr_free id)
  | ~ENVnil () => ()

fun print_env (env: !env): void = case+ env of
  | ENVcons (!id, i, !env1) =>
    (print (!id); print " = "; print i; print_newline (); print_env (!env1); fold@ env)
  | ENVnil () => (fold@ env)

fun env_find
  (env: !env, id0: strptr1): int =
  case+ env of
  | ENVcons (!id, i, !env1) => let
      extern
      fun eq_strptr_strptr (x: !strptr1, y: !strptr1)
        :<> bool = "atspre_eq_string_string"
      // end of [eq_strptr_strptr]
      overload = with eq_strptr_strptr
    in
      if id0 = !id then begin
        strptr_free id0;
        fold@ env; i
      end else let
        val res = env_find (!env1, id0) in
        fold@ env; res
      end // end of [if]
    end // end of [let]
  | ENVnil () => (strptr_free id0; fold@ env; 0)
// end of [eval_find]

fun eval_exp (env: &env, e0: exp): int = let
(*
  val () = begin
    print "eval_exp: e0 = "; print_exp e0; print_newline ()
  end // end of [val]
*)
in
  case+ e0 of
  | ~EXPint i => i
  | ~EXPadd (e1, e2) => eval_exp (env, e1) + eval_exp (env, e2)
  | ~EXPsub (e1, e2) => eval_exp (env, e1) - eval_exp (env, e2)
  | ~EXPmul (e1, e2) => eval_exp (env, e1) * eval_exp (env, e2)
  | ~EXPdiv (e1, e2) => eval_exp (env, e1) / eval_exp (env, e2)
  | ~EXPid (id) => env_find (env, id)
  | ~EXPerr () => $raise EvalErrorException
end // end of [eval_exp]

fun eval_cmd (env: &env, cmd: cmd): int =
  case+ cmd of
    | ~CMDassgn (id, e) =>
      let val i = eval_exp (env, e) in
(*
        print "eval_cmd: before:\n"; print_env env;
*)
        env := ENVcons (id, i, env);
(*
        print "eval_cmd: after:\n"; print_env env;
*)
        0
      end
    | ~CMDprint e => let
        val i = eval_exp (env, e) in
        print ">> "; print i; print_newline (); 0
      end // end of [CMDprint]
    | ~CMDquit () => 1
    | ~CMDerror () => begin
        print "The command is illegal; please try again:\n"; 0
      end // end of [CMDerror]
// end of [eval_cmd]

extern fun getline (): strptr1

fun eval_string
  (env: &env, input: strptr1): int = let
  val (pf_infil | p_infil) = infile_make_strptr input
  val (pf_lexbuf | lexbuf) = lexbuf_make_infile (pf_infil | p_infil)
  var tok: token = TOKEN (!lexbuf)
  var err: int =  (0: int)
  val cmd = parse_cmd (!lexbuf, tok, err)
(*
  val () = begin
    print "eval_string: err = "; print err; print_newline ()
  end // end of [val]
*)
  val cmd = (
    if err > 0 then (free_cmd cmd; CMDerror ()) else cmd
  ) : cmd
(*
  val () = begin
    print "eval_string: tok = "; print_token tok; print_newline ()
  end // end of [val]
*)
  val cmd = (case+ tok of
    | ~TOKeof () => cmd
    | tok_v => (free_token tok_v; free_cmd cmd; CMDerror ())
  ) : cmd
  val res = eval_cmd (env, cmd)
in
  lexbuf_free (pf_lexbuf | lexbuf); res
end // end of [eval_string]

(* ****** ****** *)

dynload "libats/lex/lexing.dats"

(* ****** ****** *)

implement
main (argc, argv) = let
//
var env0 = ENVnil ()
//
fun read_eval_print
  (env: &env >> env?): void = let
  val () = print "<< "
  val () = fflush_stdout ()
  val input = getline ()
  val i = eval_string (env, input)
in
  if i = 0 then read_eval_print (env) else (free_env env)
end // end of [read_eval_print]
//
in
//
read_eval_print (env0)
//
end // end of [main]

(* ****** ****** *)

dataviewtype charlst (int) =
  | charlst_nil (0)
  | {n:nat} charlst_cons (n+1) of (char, charlst n)
// end of [charlst]

#define nil charlst_nil
#define :: charlst_cons
#define cons charlst_cons

extern fun
charlst_is_nil {n:nat} (cs: !charlst n): bool (n == 0) =
  "charlst_is_nil"

implement
charlst_is_nil (cs) = case+ cs of
  | nil _ => (fold@ cs; true) | cons _ => (fold@ cs; false)
// end of [charlst_is_nil]

extern fun
charlst_uncons {n:pos} (cs: &charlst n >> charlst (n-1)): char =
  "charlst_uncons"

implement charlst_uncons (cs) =
  let val ~(c :: cs_r) = cs in cs := cs_r; c end
// end of [charlst_uncons]

extern fun
strptr_make_charlst_int {n:nat} (cs: charlst n, n: int n): strptr1 =
  "strptr_make_charlst_int"

%{$
ats_ptr_type
strptr_make_charlst_int (
  ats_ptr_type cs, const ats_int_type n
) {
  char *s0, *s;
  s0 = (char*)ATS_MALLOC(n+1) ;
  s = s0 + n ;
  *s = '\0' ; --s ;
  while (!charlst_is_nil(cs)) { *s = charlst_uncons(&cs) ; --s ; }
  return s0 ;
} // end of [strptr_make_charlst_int]
%} // end of [%{$]

implement getline () = let
  fun loop {n:nat} (
    cs: charlst n, n: int n
  ) : strptr1 =
    let val c = getchar () in
      if c >= 0 then begin
        case+ char_of_int c of
          | '\n' => strptr_make_charlst_int (cs, n)
          | c => loop (charlst_cons (c, cs), n+1)
      end else begin
        strptr_make_charlst_int (cs, n)
      end
    end // end of [let]
  // end of [loop]
in
  loop (charlst_nil (), 0)
end // end of [getline]

(* ****** ****** *)

(* end of [calc.lats] *)
